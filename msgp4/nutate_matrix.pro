function NUTATE_MATRIX, T
;
;     OBTAIN THE 3 X 3 ROTATION MATRIX WHICH WILL APPLY THE NUTATION
;     IN LONGITUDE (DPSI) AND IN OBLIQUITY (DEPS) AT DYNAMIC TIME T
;     (JULIAN MILLENIA SINCE J2000) TO AN OBJECT WHOSE POSITION IS
;     EXPRESSED IN RECTANGULAR EQUATORIAL COORDINATES IN THE FK5
;     SYSTEM.
;
;     B. KNAPP, 1992-06-23, 1999-06-18
;     Translated to IDL by C. Jeppesen, 2009-10-08
;
;     REFERENCE: P. K. SEIDELMANN, EXPLANATORY SUPPLEMENT TO THE ASTRO-
;     NOMICAL ALMANAC, UNIVERSITY SCIENCE BOOKS, 1992 (P. 115).
      N=dblarr(3,3)

      nut=NUTATE( T)
      DPSI=nut[0] & DEPS=nut[1]

;     NEED THE OBLIQUITY OF THE ECLIPTIC WITH AND WITHOUT NUTATION
      EPS0 = OBLIQ( T )
      EPS1 = EPS0+DEPS

;     COMPUTE THE ROTATION MATRIX
      SDP = SIN(DPSI)
      CDP = COS(DPSI)
      SE0 = SIN(EPS0)
      CE0 = COS(EPS0)
      SE1 = SIN(EPS1)
      CE1 = COS(EPS1)

      N[0,0] =  CDP
      N[0,1] = -CE0*SDP
      N[0,2] = -SE0*SDP
      N[1,0] =  CE1*SDP
      N[1,1] =  CE0*CE1*CDP+SE0*SE1
      N[1,2] =  SE0*CE1*CDP-CE0*SE1
      N[2,0] =  SE1*SDP
      N[2,1] =  CE0*SE1*CDP-SE0*CE1
      N[2,2] =  SE0*SE1*CDP+CE0*CE1

      RETURN,N

END
