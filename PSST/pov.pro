pro pov,oufn,t,et0,xyz,vxyz,axyz,rho,alt,agl,csound,alpha,beta,pxform=pxform
  openw,ouf,oufn,/get
  printf,ouf,'//Generated by pov.pro '+systime(/utc)+'UTC'
  printf,ouf,'//Chris Jeppesen, Kwan Systems, Longmont, CO'
  printf,ouf,'//All vectors in IAU_MARS Mars-fixed frame unless otherwise stated'
  printf,ouf,'//All distances in meters, all time in seconds, all angles in radians, all derived units in SI primary derived units'
  printf,ouf,'//i - zero-based frame number'
  printf,ouf,'//utc - SCET(UTC) of this frame, in ISO date format'
  printf,ouf,'//et - SCET(ET) of this frame, in seconds from J2000 ET epoch'
  printf,ouf,'//r - Vector from center of Mars to spacecraft'
  printf,ouf,'//v - Relative velocity vector (Airspeed, ignoring local wind)'
  printf,ouf,'//a - Non-gravitational acceleration'
  printf,ouf,'//s - Vector from spacecraft to center of Sun corrected for LT+S (direction sun appears in sky)'
  printf,ouf,'//e - Vector from spacecraft to center of Earth (not Earth-Moon Barycenter) corrected for XLT+S (direction to aim transmission in order to hit Earth)'
  if n_elements(pxform) eq 0 then begin
    printf,ouf,'//rho,agl,alt,csound - Atmospheric density, altitude above ground level, altitude above areoid'
    printf,ouf,'//alpha,bank - pitch and roll about relative velocity vector'
  end else begin
    printf,ouf,'//c11,c12,c13 - first  row of body->IAU_MARS matrix'
    printf,ouf,'//c21,c22,c23 - second row of body->IAU_MARS matrix'
    printf,ouf,'//c31,c32,c33 - third  row of body->IAU_MARS matrix'
  end
  cspice_etcal,et0,etcal
  cspice_et2utc,et0,"ISOC",3,utccal
  cspice_spkezr,'399'  ,t+et0,   'IAU_MARS','XLT+S','MSL',earth,ltime
  earth=transpose(earth)*1000d; 
  cspice_spkezr,'SUN'  ,t+et0,   'IAU_MARS','LT+S','MSL',sun,ltime
  sun=transpose(sun)*1000d; 
  printf,ouf,format='(%"//Time from %16.4f (%sTDB, %sUTC)")',et0,etcal,utccal
  hh0=long(strmid(utccal,11,2))
  nn0=fix(strmid(utccal,14,2))
  ss0=double(strmid(utccal,17,6))
  printf,ouf,format='(%"#declare StartUTC=\"%s\";")',utccal
  printf,ouf,format='(%"#declare StartUTCSOD=%9.3f;")',double(hh0*3600+nn0*60)+ss0
  if n_elements(pxform) eq 0 then begin
    printf,ouf,format='(%"array[%d][23] {")',n_elements(t)
    printf,ouf,'//    i utc                          et                    deltat                rx                  ry                 rz                 vx                 vy                 vz                 ax                 ay                 az                 sx                 sy                 sz                 ex                 ey                 ez                 rho                agl                alt                csound             alpha              bank'
    printf,ouf,'//                                   0                     1                      2                   3                  4                  5                  6                  7                  8                  9                 10                 11                 12                 13                 14                 15                 16                 17                 18                 19                 20                 21                 22'
  end else begin
    printf,ouf,format='(%"array[%d][26] {")',n_elements(t)
    printf,ouf,'//    i utc                          et                    deltat                rx                  ry                 rz                 vx                 vy                 vz                 ax                 ay                 az                 sx                 sy                 sz                 ex                 ey                 ez                 c11                c12                c13                c21                c22                c23                c31                c32                c33'
    printf,ouf,'//                                   0                     1                      2                   3                  4                  5                  6                  7                  8                  9                 10                 11                 12                 13                 14                 15                 16                 17                 18                 19                 20                 21                 22                 23                 24                 25'
  end
  for i=0,n_elements(t)-1 do begin
    cspice_et2utc,t[i]+et0,"ISOC",3,utccal
    if n_elements(pxform) eq 0 then begin
      printf,ouf,format='(%"/*%5d %sUTC*/{%21.14e,%21.14e,'+ $
                        '%18.10e,%18.10e,%18.10e,'+        $ ;R
                        '%18.10e,%18.10e,%18.10e,'+        $ ;V
                        '%18.10e,%18.10e,%18.10e,'+        $ ;A
                        '%18.10e,%18.10e,%18.10e,'+        $ ;S
                        '%18.10e,%18.10e,%18.10e,'+        $ ;E
                        '%18.10e,%18.10e,%18.10e,%18.10e,'+$ ;rho,agl,alt,csound
                        '%18.10e,%18.10e},")',             $ ;alpha,beta
                         i,utccal,t[i]+et0,t[i],$
                         xyz  [i,0],xyz  [i,1],xyz  [i,2], $
                         vxyz [i,0],vxyz [i,1],vxyz [i,2], $
                         axyz [i,0],axyz [i,1],axyz [i,2], $
                         sun  [i,0],sun  [i,1],sun  [i,2], $
                         earth[i,0],earth[i,1],earth[i,2], $
                         rho  [i]  ,agl  [i]  ,alt  [i]  ,csound[i], $
                         alpha[i]  ,beta [i]
    end else begin                         
      printf,ouf,format='(%"/*%5d %sUTC*/{%21.14e,%21.14e,'+ $
                        '%18.10e,%18.10e,%18.10e,'+        $ ;R
                        '%18.10e,%18.10e,%18.10e,'+        $ ;V
                        '%18.10e,%18.10e,%18.10e,'+        $ ;A
                        '%18.10e,%18.10e,%18.10e,'+        $ ;S
                        '%18.10e,%18.10e,%18.10e,'+        $ ;E
                        '%18.10e,%18.10e,%18.10e,'+        $ ;c11,c12,c13
                        '%18.10e,%18.10e,%18.10e,'+        $ ;c21,c22,c23
                        '%18.10e,%18.10e,%18.10e,},")',    $ ;c31,c32,c33
                         i,utccal,t[i]+et0,t[i],$
                         xyz  [i,0],xyz  [i,1],xyz  [i,2], $
                         vxyz [i,0],vxyz [i,1],vxyz [i,2], $
                         axyz [i,0],axyz [i,1],axyz [i,2], $
                         sun  [i,0],sun  [i,1],sun  [i,2], $
                         earth[i,0],earth[i,1],earth[i,2], $
                         pxform[0,0,i],pxform[1,0,i],pxform[2,0,i], $
                         pxform[0,1,i],pxform[1,1,i],pxform[2,1,i], $
                         pxform[0,2,i],pxform[1,2,i],pxform[2,2,i]
    end
  end  
  printf,ouf,'}'

  free_lun,ouf
end